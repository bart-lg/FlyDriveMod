breed [ flies fly ]

; properties of fly
flies-own [
  
  ; modes: stationary (yellow), adult (red)
  mode
  ; days since last mode change
  mode-duration
  
  ; generation (1 (initial population), 2, 3, ...)
  generation
  
  ; sex: f,m
  sex
  
  ; partner-search: TRUE, FALSE (if false => color: blue)
  partner-search
  
  ; number of eggs in body
  eggs
  
  ; tick of last egg laying
  last-egg-tick
  
  ; own genotype: ++ | M+ | MM | R+ | RM | RR
  ; + => wild type
  ; M => modified type
  ; R => resistant type
  genotype
  
  ; genotype of partner (important for determining genotype of next generation)
  genotype-of-partner
  
  ; following properties in ticks
  total-age
  ticks-since-fertilization
  
]

to set-mortality-rates
  
  ; first check if csv file exists
  let file-path (word path-csv-input "mortalityRates.csv")
  if file-exists? file-path [
  
    file-open file-path
    
    ; check the headers of the columns
    let headers csv:from-row file-read-line
    if item 0 headers = "temperature" and item 1 headers = "mortalityRate" [
    
      ; create arrays
      set mortality-temperatures     array:from-list []
      set stat-mortality-rates-temp   array:from-list []
      set adult-mortality-rates-temp array:from-list []
      
      ; read data
      while [ not file-at-end? ] [
        
        let row csv:from-row file-read-line
        
        ; set temperature steps
        set mortality-temperatures array:from-list lput item 0 row array:to-list mortality-temperatures
        
        ; set mortality-rates depending on temperature
        set stat-mortality-rates-temp  array:from-list lput item 1 row array:to-list stat-mortality-rates-temp
        set adult-mortality-rates-temp array:from-list lput item 1 row array:to-list adult-mortality-rates-temp
        
      ]
      
    ]
    
    file-close
    
  ]
  
end

to set-eggs-per-day-rates

  ; first check if csv file exists
  let file-path (word path-csv-input "eggsPerDay.csv")
  if file-exists? file-path [
  
    file-open file-path
    
    ; check the headers of the columns
    let headers csv:from-row file-read-line
    if item 0 headers = "temperature" and item 1 headers = "eggsPerDay" [
    
      ; create arrays
      set eggs-per-day-temperatures array:from-list []
      set eggs-per-day array:from-list []
      
      ; read data
      while [ not file-at-end? ] [
        
        let row csv:from-row file-read-line
        
        ; set temperature steps
        set eggs-per-day-temperatures array:from-list lput item 0 row array:to-list eggs-per-day-temperatures
        
        ; set eggs-per-day-rates depending on temperature
        set eggs-per-day array:from-list lput item 1 row array:to-list eggs-per-day
        
      ]
      
    ]
    
    file-close
    
  ]
  
end

to set-mode-durations
  
  let allFilesExist TRUE
  
  ; check if all csv files exist
  let file-path (word path-csv-input "femaleEggsToAdult.csv")
  if not file-exists? file-path [ set allFilesExist FALSE ]
  set file-path (word path-csv-input "femaleAdultLongevity.csv")
  if not file-exists? file-path [ set allFilesExist FALSE ]
  set file-path (word path-csv-input "maleEggsToAdult.csv")
  if not file-exists? file-path [ set allFilesExist FALSE ]
  set file-path (word path-csv-input "maleEggsToPupa.csv")
  if not file-exists? file-path [ set allFilesExist FALSE ]
  
  ; ### TEMPERATURES
  ; ### FEMALE EGGS TO ADULT
  
  set file-path (word path-csv-input "femaleEggsToAdult.csv")
  file-open file-path
  
  ; check the headers of the columns
  let headers csv:from-row file-read-line
  if item 0 headers = "temperature" and item 1 headers = "femaleEggsToAdult" [
  
    ; create arrays
    set mode-duration-temperatures array:from-list []
    set female-stat-durations      array:from-list []
    
    ; read data
    while [ not file-at-end? ] [
      
      let row csv:from-row file-read-line
      
      ; set temperature steps
      set mode-duration-temperatures array:from-list lput item 0 row array:to-list mode-duration-temperatures
      
      ; set female-egg-to-pupa-durations depending on temperature
      set female-stat-durations      array:from-list lput item 1 row array:to-list female-stat-durations
      
    ]  
    
  ]
  
  file-close
  
  ; ### FEMALE ADULT LONGEVITY
  
  set file-path (word path-csv-input "femaleAdultLongevity.csv")
  file-open file-path
  
  ; check the headers of the columns
  set headers csv:from-row file-read-line
  if item 0 headers = "temperature" and item 1 headers = "femaleAdultLongevity" [
  
    ; create arrays
    set female-adult-longevities array:from-list []
    
    ; read data
    while [ not file-at-end? ] [
      
      let row csv:from-row file-read-line
      
      ; set female-egg-to-pupa-durations depending on temperature
      set female-adult-longevities array:from-list lput item 1 row array:to-list female-adult-longevities
      
    ]  
    
  ]  
  
  file-close  
  
  ; ### MALE EGGS TO ADULT
  
  set file-path (word path-csv-input "maleEggsToAdult.csv")
  file-open file-path
  
  ; check the headers of the columns
  set headers csv:from-row file-read-line
  if item 0 headers = "temperature" and item 1 headers = "maleEggsToAdult" [
  
    ; create arrays
    set male-stat-durations array:from-list []
    
    ; read data
    while [ not file-at-end? ] [
      
      let row csv:from-row file-read-line
      
      ; set female-egg-to-pupa-durations depending on temperature
      set male-stat-durations array:from-list lput item 1 row array:to-list male-stat-durations
      
    ]  
    
  ]
  
  file-close
  
  ; ### MALE ADULT LONGEVITY
  
  set file-path (word path-csv-input "maleAdultLongevity.csv")
  file-open file-path
  
  ; check the headers of the columns
  set headers csv:from-row file-read-line
  if item 0 headers = "temperature" and item 1 headers = "maleAdultLongevity" [
  
    ; create arrays
    set male-adult-longevities array:from-list []
    
    ; read data
    while [ not file-at-end? ] [
      
      let row csv:from-row file-read-line
      
      ; set female-egg-to-pupa-durations depending on temperature
      set male-adult-longevities array:from-list lput item 1 row array:to-list male-adult-longevities
      
    ]  
    
  ]  
  
  file-close  
  
end

to get-current-mode-durations
  
  ; determine index for arrays
  let i 0
  let stop-flag FALSE
  while [ not stop-flag ] [
    ifelse ( array:item mode-duration-temperatures i ) >= ( round mean-10d-temp ) [ 
      set stop-flag TRUE
    ] [
      ifelse i < ( array:length mode-duration-temperatures - 1 ) [
        set i ( i + 1 ) 
      ] [
        set stop-flag TRUE 
      ]
    ]
  ]
  
  set current-female-stat-duration   array:item female-stat-durations    i
  set current-female-adult-longevity array:item female-adult-longevities i
  set current-male-stat-duration     array:item male-stat-durations      i
  set current-male-adult-longevity   array:item male-adult-longevities   i
  
end

to calculate-current-eggs-per-tick-rate
  
  ; determine index for array
  let i 0
  let stop-flag FALSE
  while [ not stop-flag ] [
    ifelse ( array:item eggs-per-day-temperatures i ) >= ( round mean-10d-temp ) [ 
      set stop-flag TRUE
    ] [
      ifelse i < ( array:length eggs-per-day-temperatures - 1 ) [
        set i ( i + 1 ) 
      ] [
        set stop-flag TRUE 
      ]
    ]
  ]  
  
  ; calculate current eggs-per-tick-rate
  set current-eggs-per-tick ( array:item eggs-per-day i ) / ticks-per-day
  
  ; log values
  array:set output-values 38 ( array:item eggs-per-day i )
  array:set output-values 39 current-eggs-per-tick
  
end

to fly-init-pop
  
  set-default-shape flies "dot"
  
  ask n-of init-pop patches [
    sprout-flies 1 [
      set color red
      set mode "adult"
      set mode-duration 0
      set generation 1
      ifelse ( random 2 ) = 1 [ set sex "f" ][ set sex "m" ]
      set partner-search TRUE
      set eggs 0
      set genotype "++"
      set total-age 0
      set ticks-since-fertilization 0
    ] 
  ]

  ask n-of ( init-pop * ( resistant-ratio / 100 ) ) flies [
    set genotype "RR"
  ]
  
end

to kill-flies-off-season
  ask n-of ( ceiling ( count flies * mortality-off-season ) ) flies [ die ]
end

to kill-flies
  
  ; ### death due to life-expectancy ###

  let female-adult-flies count flies with [ sex = "f" and mode = "adult" ]
  let male-adult-flies   count flies with [ sex = "m" and mode = "adult" ]
  
  ask flies [
    if sex = "f" and mode = "adult" and current-female-adult-longevity < ( mode-duration / ticks-per-day ) [ die ]
    if sex = "m" and mode = "adult" and current-male-adult-longevity   < ( mode-duration / ticks-per-day ) [ die ]
  ]
  ; log
  array:set output-values 48 ( female-adult-flies - ( count flies with [ sex = "f" and mode = "adult" ] ) )
  array:set output-values 49 ( male-adult-flies   - ( count flies with [ sex = "m" and mode = "adult" ] ) )
  
  ; ### death due to mortality rate ###
  
  ; determine index for arrays
  let i 0
  let stop-flag FALSE
  while [ not stop-flag ] [
    ifelse ( array:item mortality-temperatures i ) >= ( round mean-10d-temp ) [ 
      set stop-flag TRUE
    ] [
      ifelse i < ( array:length mortality-temperatures - 1 ) [
        set i ( i + 1 ) 
      ] [
        set stop-flag TRUE 
      ]
    ]
  ]
  
  ; calculate mortality rates for current day
  ; endpop = startpop * exp^(k * t)
  ; t => 1 (one day)
  ; k = ln(1 - mortality-rate) / interval
  
  ; we need to "let" a variable outside of if statement...
  let stat-mortality-rate 1
  if array:item stat-mortality-rates-temp i   < 1 [
    set stat-mortality-rate   1 - ( e ^ ( ln ( 1 - array:item stat-mortality-rates-temp i )   / mortality-interval ) )
  ]
  ; log
  array:set output-values 46 stat-mortality-rate
  
  let adult-mortality-rate 1
  if array:item adult-mortality-rates-temp i < 1 [
    set adult-mortality-rate 1 - ( e ^ ( ln ( 1 - array:item adult-mortality-rates-temp i ) / mortality-interval ) )
  ]
  ; log
  array:set output-values 47 adult-mortality-rate
  
  let PP-stat-flies  count flies with [ mode = "stationary" and genotype = "++" ]
  let PP-adult-flies count flies with [ mode = "adult"      and genotype = "++" ]
  
  let PR-stat-flies  count flies with [ mode = "stationary" and ( genotype = "R+" or genotype = "+R" ) ]
  let PR-adult-flies count flies with [ mode = "adult"      and ( genotype = "R+" or genotype = "+R" ) ]

  let RR-stat-flies  count flies with [ mode = "stationary" and genotype = "RR" ]
  let RR-adult-flies count flies with [ mode = "adult"      and genotype = "RR" ]

  let MM-stat-flies  count flies with [ mode = "stationary" and genotype = "MM" ]
  let MM-adult-flies count flies with [ mode = "adult"      and genotype = "MM" ]

  let MR-stat-flies  count flies with [ mode = "stationary" and ( genotype = "MR" or genotype = "RM" ) ]
  let MR-adult-flies count flies with [ mode = "adult"      and ( genotype = "MR" or genotype = "RM" ) ]

  let MP-stat-flies  count flies with [ mode = "stationary" and ( genotype = "M+" or genotype = "+M" ) ]
  let MP-adult-flies count flies with [ mode = "adult"      and ( genotype = "M+" or genotype = "+M" ) ]
  
  
  ; kill flies due to mortality rate
  
  let kill-num ( ceiling ( PP-stat-flies   * stat-mortality-rate   * ( 1 + ( 1 - fitness-PP ) ) ) )   
  ask n-of kill-num flies with [mode = "stationary"   and genotype = "++" ] [ die ]
  ; log
  array:set output-values 50 kill-num
  
  set kill-num ( ceiling ( PP-adult-flies * adult-mortality-rate * ( 1 + ( 1 - fitness-PP ) ) ) )
  ask n-of kill-num flies with [mode = "adult" and genotype = "++" ] [ die ]
  ; log
  array:set output-values 51 kill-num


  set kill-num ( ceiling ( PR-stat-flies   * stat-mortality-rate   * ( 1 + ( 1 - fitness-PR ) ) ) )
  ask n-of kill-num flies with [mode = "stationary"   and ( genotype = "R+" or genotype = "+R" ) ] [ die ]
  ; log
  array:set output-values 52 kill-num
  
  set kill-num ( ceiling ( PR-adult-flies * adult-mortality-rate * ( 1 + ( 1 - fitness-PR ) ) ) )
  ask n-of kill-num flies with [mode = "adult" and ( genotype = "R+" or genotype = "+R" ) ] [ die ]
  ; log
  array:set output-values 53 kill-num
  

  set kill-num ( ceiling ( RR-stat-flies   * stat-mortality-rate   * ( 1 + ( 1 - fitness-RR ) ) ) )
  ask n-of kill-num flies with [mode = "stationary"   and genotype = "RR" ] [ die ]
  ; log
  array:set output-values 54 kill-num
  
  set kill-num ( ceiling ( RR-adult-flies * adult-mortality-rate * ( 1 + ( 1 - fitness-RR ) ) ) )
  ask n-of kill-num flies with [mode = "adult" and genotype = "RR" ] [ die ]
  ; log
  array:set output-values 55 kill-num
  

  set kill-num ( ceiling ( MM-stat-flies   * stat-mortality-rate   * ( 1 + ( 1 - fitness-MM ) ) ) )
  ask n-of kill-num flies with [mode = "stationary"   and genotype = "MM" ] [ die ]
  ; log
  array:set output-values 56 kill-num
  
  set kill-num ( ceiling ( MM-adult-flies * adult-mortality-rate * ( 1 + ( 1 - fitness-MM ) ) ) )
  ask n-of kill-num flies with [mode = "adult" and genotype = "MM" ] [ die ]
  ; log
  array:set output-values 57 kill-num


  set kill-num ( ceiling ( MP-stat-flies   * stat-mortality-rate   * ( 1 + ( 1 - fitness-PP ) ) ) )
  ask n-of kill-num flies with [mode = "stationary"   and ( genotype = "M+" or genotype = "+M" ) ] [ die ]
  ; log
  array:set output-values 58 kill-num  
  
  set kill-num ( ceiling ( MP-adult-flies * adult-mortality-rate * ( 1 + ( 1 - fitness-PP ) ) ) )
  ask n-of kill-num flies with [mode = "adult" and ( genotype = "M+" or genotype = "+M" ) ] [ die ]  
  ; log
  array:set output-values 59 kill-num  
  

  set kill-num ( ceiling ( MR-stat-flies   * stat-mortality-rate   * ( 1 + ( 1 - fitness-MR ) ) ) )
  ask n-of kill-num flies with [mode = "stationary"   and ( genotype = "MR" or genotype = "RM" ) ] [ die ]
  ; log
  array:set output-values 60 kill-num
  
  set kill-num ( ceiling ( MR-adult-flies * adult-mortality-rate * ( 1 + ( 1 - fitness-MR ) ) ) )
  ask n-of kill-num flies with [mode = "adult" and ( genotype = "MR" or genotype = "RM" ) ] [ die ]  
  ; log
  array:set output-values 61 kill-num  
  
end

to fly-activities
  ask flies [

    reset-timer
    
    ; upgrade mode of fly
    upgrade-fly
    
    if timer > array:item time-measurements 3 [ array:set time-measurements 3 timer ]
    
    let ready-to-lay-egg FALSE
    if current-eggs-per-tick > 0 and sex = "f" and eggs > 0 and ticks-since-fertilization > egg-dev-duration and ( ticks - last-egg-tick ) >= round ( 1 / current-eggs-per-tick ) [
      set ready-to-lay-egg TRUE
    ]
    
    ; if female fly detects male fly on same patch => start fertilization
    if sex = "f" and mode = "adult" and partner-search = TRUE and any? flies-here with [sex = "m"] [
      set partner-search FALSE
      set ticks-since-fertilization 0
      set eggs eggs-per-cycle
      set genotype-of-partner first [genotype] of n-of 1 flies-here with [sex = "m"] 
      set color blue
    ]
    
    ; lay egg
    if ready-to-lay-egg [ 

      reset-timer
      
      let cherries-here ( trees-on patches in-radius floor ( tree-width / 2 ) ) with [grown-cherries > 0]
      let fruits-here ( yummy-plants-on patch-here ) with [grown-fruits > 0]
      
      if any? cherries-here or any? fruits-here [
      
        ; save generation number in separate variable to make it accessible in sprout statement
        let generation-temp generation
        
        let genotype-temp new-genotype genotype genotype-of-partner
        
        ; FEMALE: M+ or MR => ++ or R+ DIE
        ; FEMALE: M+ or MR => RR DIE with certain prob. due to resistance
        let lay-egg TRUE
        if member? "M" genotype [
          if genotype-temp = "++" or genotype-temp = "R+" or genotype-temp = "+R" [ set lay-egg FALSE ]
          if genotype-temp = "RR" [
            if random-float 1 > resistance-rate [
              set lay-egg FALSE
            ]
          ] 
        ]
        
        ifelse lay-egg [
          
          ask patch-here [
            sprout-flies 1 [
              set color yellow
              set mode "stationary"
              set mode-duration 0
              set generation generation-temp + 1
              ifelse ( random 2 ) = 1 [ set sex "f" ][ set sex "m" ]
              set partner-search FALSE
              set eggs 0
              set genotype genotype-temp
              set total-age 0
              set ticks-since-fertilization 0
            ]
          ]
          
          ifelse any? cherries-here [
            
            ask one-of cherries-here [
              set grown-cherries ( grown-cherries - 1 )
              set occupied-cherries ( occupied-cherries + 1 )
              update-tree-label
            ]
            
          ] [
            
            ifelse any? fruits-here [
              
              ask one-of fruits-here [
                set grown-fruits ( grown-fruits - 1 ) 
                set occupied-fruits ( occupied-fruits + 1 )
              ]
              
            ] [
              print "Laid egg on patch with no cherries and no fruit (should not happen!)" 
            ]
            
          ]
          
          ; log
          array:set output-values 40 ( ( array:item output-values 40 ) + 1 )
          
        ] [
          ; log
          array:set output-values 41 ( ( array:item output-values 41 ) + 1 )
        ]
        
        set eggs ( eggs - 1 )
        set last-egg-tick ticks
        
        if eggs = 0 [
          set partner-search TRUE
          set color red
        ] 
        
      ]
      
      if timer > array:item time-measurements 4 [ array:set time-measurements 4 timer ]
      
    ]
    
    ; in case of fertilization => increase ticks since fertilization
    if partner-search = FALSE and sex = "f" [ set ticks-since-fertilization ( ticks-since-fertilization + 1 ) ]

    ; increase age and mode-duration
    set mode-duration ( mode-duration + 1 )
    set total-age ( total-age + 1 )
    
    reset-timer
    
    ; female flies with eggs can sens cherries and fruits in a distance of 15m
    ifelse ready-to-lay-egg [
      
      let visible-cherries trees with [grown-cherries > 0] in-radius visibility
      let visible-fruits yummy-plants with [grown-fruits > 0] in-radius visibility
      
      ifelse any? visible-cherries [
        
        ; do not move if cherries an same patch
        if not any? trees-here with [grown-cherries > 0] [
          face min-one-of visible-cherries [ distance myself ]
          fd 1
        ]
        
      ] [
       
        ifelse any? visible-fruits [
          
          ; do not move if fruits on same patch
          if not any? yummy-plants-here with [grown-fruits > 0] [
            face min-one-of visible-fruits [ distance myself ]
            fd 1
          ]
          
        ] [
          
          ; move randomly if no fruits in visibility range
          rt random 360
          fd 1 
          
        ]
        
      ]
      
    ] [
      
      ; all other flies move randomly
      if mode = "adult" [
        rt random 360
        fd 1
      ]
      
    ]
    
    if timer > array:item time-measurements 5 [ array:set time-measurements 5 timer ]
    
  ]  
end

; upgrade mode of fly if appropriate mode duration reached
to upgrade-fly
  
  if mode = "stationary" [
    
    if ( sex = "f" and mode = "stationary" and round (mode-duration / ticks-per-day) >= current-female-stat-duration ) [
      set mode "adult"
      set mode-duration 0
      set partner-search TRUE
      set color red
    ]
    
    if ( sex = "m" and mode = "stationary" and round (mode-duration / ticks-per-day) >= current-male-stat-duration ) [
      set mode "adult"
      set mode-duration 0
      set partner-search TRUE
      set color red
    ]
    
    ; increase log-value
    if mode = "adult" [ array:set output-values 37 ( ( array:item output-values 37 ) + 1 ) ]
    
  ]

end

; calculates new genotype
; M+ => 50% prob. M; 50% prob. +
to-report new-genotype [genotype1 genotype2]
  
  let temp1 ""
  let temp2 ""
  
  if genotype1 = "++" [ set temp1 "+" ]
  if genotype1 = "MM" [ set temp1 "M" ]
  if genotype1 = "RR" [ set temp1 "R" ]
  
  if genotype1 = "M+" or genotype1 = "+M" [ ifelse random 2 = 1 [ set temp1 "M" ] [ set temp1 "+" ] ]
  if genotype1 = "R+" or genotype1 = "+R" [ ifelse random 2 = 1 [ set temp1 "R" ] [ set temp1 "+" ] ]
  if genotype1 = "MR" or genotype1 = "RM" [ ifelse random 2 = 1 [ set temp1 "M" ] [ set temp1 "R" ] ]
  
  if genotype2 = "++" [ set temp2 "+" ]
  if genotype2 = "MM" [ set temp2 "M" ]
  if genotype2 = "RR" [ set temp2 "R" ]
  
  if genotype2 = "M+" or genotype2 = "+M" [ ifelse random 2 = 1 [ set temp2 "M" ] [ set temp2 "+" ] ]
  if genotype2 = "R+" or genotype2 = "+R" [ ifelse random 2 = 1 [ set temp2 "R" ] [ set temp2 "+" ] ]
  if genotype2 = "MR" or genotype2 = "RM" [ ifelse random 2 = 1 [ set temp2 "M" ] [ set temp2 "R" ] ]
  
  report (word temp1 temp2)
  
end

to release-gene-drive
  
  ask patch ( max-pxcor / 2 ) ( max-pycor / 2 ) [
    sprout-flies release-amount [
      set color red
      set mode "adult"
      set mode-duration 0
      set generation 1
      ; ifelse ( random 2 ) = 1 [ set sex "f" ][ set sex "m" ]
      set sex "f"
      set partner-search TRUE
      set eggs 0
      set genotype "MM"
      set total-age 0
      set ticks-since-fertilization 0
    ] 
  ]  
  
end