breed [ trees tree ]

; properties of tree
trees-own [
  
  max-cherries
  grown-cherries
  occupied-cherries
  
]

to plant-trees
  
  ; coordinates in world
  let x-coordinate 0
  let y-coordinate 0
  
  ; tree indices
  let i 1
  let j 1 
  
  while [ i <= tree-rows ] [
    
    set j 1
    while [ j <= trees-per-row ] [
      
      set x-coordinate ( border-margin + wildberry-plant-width + wildberry-plant-margin + ( j - 1 ) * ( tree-width + tree-margin-x ) )
      set y-coordinate ( border-margin + wildberry-plant-width + wildberry-plant-margin + ( i - 1 ) * ( tree-width + tree-margin-y ) )
      
      if ( tree-width > 1 ) [ set x-coordinate ( x-coordinate + ceiling ( tree-width / 2 ) ) ]
      if ( tree-width > 1 ) [ set y-coordinate ( y-coordinate + ceiling ( tree-width / 2 ) ) ]
      
      create-trees 1 [
        set shape "circle"
        set color green
        set size tree-width 
        setxy x-coordinate y-coordinate
        set max-cherries round ( random-normal mean-cherries sd-cherries )
        set grown-cherries 0
        set occupied-cherries 0
        update-tree-label     
      ]
      
      set j ( j + 1 ) 
    ]
    
    set i ( i + 1 ) 
  ]
  
end

to update-tree-label
  set label ( word grown-cherries "|" occupied-cherries )
end

to grow-cherries
  
  let day 1
  if ( ticks > 0 ) [
    set day ( ceiling ( ticks / ticks-per-day ) mod 365 )
  ]
  
  if day = 1 [
    ask trees [
      set grown-cherries 0
      set occupied-cherries 0
      update-tree-label
    ]
  ]
  
  if ( day >= cherries-growth-start ) and ( day <= cherries-growth-start + cherries-growth-period ) [
    
    ifelse ( day = cherries-growth-start + cherries-growth-period ) [
      ask trees [
        if ( max-cherries - occupied-cherries ) > grown-cherries [
          set grown-cherries ( max-cherries - occupied-cherries ) 
        ]
      ]
    ] [
      ask trees [
        let new-cherries ceiling ( max-cherries / ( cherries-growth-period * ticks-per-day ) )
        ifelse ( grown-cherries + new-cherries ) < ( max-cherries - occupied-cherries ) [
          set grown-cherries ( grown-cherries + new-cherries ) 
        ] [
          if ( max-cherries - occupied-cherries ) > grown-cherries [
            set grown-cherries ( max-cherries - occupied-cherries ) 
          ]
        ]  
      ]
    ]  
    
    ask trees [ update-tree-label ]
    
  ]
  
  set total-grown-cherries sum [ grown-cherries ] of trees
  ifelse total-grown-cherries > 0 [ set cherries-available TRUE ] [ set cherries-available FALSE ]
    
end

to cherries-attract-closest-flies
  
  if cherries-available [
    
    ask trees with [ grown-cherries > 0 ] [
     
      let flies-in-radius flies with [ ready-to-lay-egg and ( target-tick < ticks or distance myself < target-distance ) ] in-radius visibility
      if count flies-in-radius > grown-cherries [
        set flies-in-radius min-n-of grown-cherries flies-in-radius [ distance myself ]
      ]
      
      let target-x xcor
      let target-y ycor
      
      ask flies-in-radius [
       
        facexy target-x target-y
        set target-tick ticks
        set target-distance distance patch target-x target-y
        
      ]
      
    ]
    
  ]
  
end